plugins {
    id 'java'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
    id 'com.google.protobuf' version '0.8.10'
    // Provide convenience executables for trying out the examples.
    id 'application'
}

group 'sarthakn.starter'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceCompatibility = 14
targetCompatibility = 14

def grpcVersion = '1.30.0'
def protobufVersion = '3.12.0'

dependencies {
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

startScripts.enabled = false

task buildServer(type: CreateStartScripts) {
    mainClassName = 'sarthakn.starter.server.Application'
    applicationName = 'starter-server'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

task buildClient(type: CreateStartScripts) {
    mainClassName = 'sarthakn.starter.client.TestClient'
    applicationName = 'starter-client'
    outputDir = new File(project.buildDir, 'tmp')
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(buildServer)
    from(buildClient)
    fileMode = 0755
}